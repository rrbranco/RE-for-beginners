\subsection{Carregando uma constante 32-bits no registrador}
\label{MIPS_big_constants}

\begin{lstlisting}[style=customc]
unsigned int f()
{
	return 0x12345678;
};
\end{lstlisting}

Todas as intruções no MIPS, iguais ao ARM, tem o tamanho de 32-bits, então não é possível embutir uma constante de 32-bits em uma única instrução.
\myindex{MIPS!\Instructions!LI}
\myindex{MIPS!\Instructions!ORI}

Então, faz-se necessário utilizar ao menos duas intruções:
A primeira carrega a parte mais alta do número de 32-bits e a segunda realiza uma operação OR, o que fetivamente seta a parte baixa de 16-bits do registrador alvo:

\begin{lstlisting}[caption=GCC 4.4.5 -O3 (\assemblyOutput),style=customasmMIPS]
        li      $2,305397760  # 0x12340000
        j       $31
        ori     $2,$2,0x5678 ; branch delay slot
\end{lstlisting}

\IDA é sabe que este padrão é encontrado frequentemente, então por confveniência mostra apenas a última \INS{ORI} instrução como a \INS{LI} pseudo-instrução,
O que supostamente carrega um número de 32-btis completo dentro do \$V0 registrador.

\myindex{MIPS!\Instructions!LUI}

\begin{lstlisting}[caption=GCC 4.4.5 -O3 (IDA),style=customasmMIPS]
         lui     $v0, 0x1234
         jr      $ra
         li      $v0, 0x12345678 ; branch delay slot
\end{lstlisting}

A saída assemby do GCC tem o pseudo-instrução LI, masm na verdade, \INS{LUI} (\q{Load Upper Immediate}) está lá,
o que armazena o valor de 16-bits na parte alta do registrador.

Vejamos a saída no \IT{objdump}:

\begin{lstlisting}[caption=objdump,style=customasmMIPS]
00000000 <f>:
   0:   3c021234        lui     v0,0x1234
   4:   03e00008        jr      ra
   8:   34425678        ori     v0,v0,0x5678
\end{lstlisting}

\subsubsection{Carregando uma variável global de 32-bits no registrador}

\begin{lstlisting}[style=customc]
unsigned int global_var=0x12345678;

unsigned int f2()
{
        return global_var;
};
\end{lstlisting}

\myindex{MIPS!\Instructions!LW}

É um pouco diferente: \INS{LUI} carrega a parte superior de 16-bits do \IT{global\_var} no \$2 (ou \$V0) e então \INS{LW} carrega a parte inferior de 16-bits somando o seu conteúdo com o conteúdo de \$2:

\begin{lstlisting}[caption=GCC 4.4.5 -O3 (\assemblyOutput),style=customasmMIPS]
f2:
        lui     $2,%hi(global_var)
        lw      $2,%lo(global_var)($2)
        j       $31
        nop	; branch delay slot

	...

global_var:
        .word   305419896
\end{lstlisting}

\IDA sabe quão frequente é usado o par de instruções \INS{LUI}/\INS{LW} e concatena ambas em uma única instruçã \INS{LW}:

\begin{lstlisting}[caption=GCC 4.4.5 -O3 (IDA),style=customasmMIPS]
_f2:
                lw      $v0, global_var
                jr      $ra
                or      $at, $zero	; branch delay slot

		...

                .data
                .globl global_var
global_var:     .word 0x12345678         # DATA XREF: _f2
\end{lstlisting}

A saída do \IT{objdump} é a mesm do assembly do GCC.
Vamos também exibir o "relocs" no arquivo objeto:

\begin{lstlisting}[caption=objdump,style=customasmMIPS]
objdump -D filename.o

...

0000000c <f2>:
   c:   3c020000        lui     v0,0x0
  10:   8c420000        lw      v0,0(v0)
  14:   03e00008        jr      ra
  18:   00200825        move    at,at	; branch delay slot
  1c:   00200825        move    at,at

Disassembly of section .data:

00000000 <global_var>:
   0:   12345678        beq     s1,s4,159e4 <f2+0x159d8>

...

objdump -r filename.o

...

RELOCATION RECORDS FOR [.text]:
OFFSET   TYPE              VALUE
0000000c R_MIPS_HI16       global_var
00000010 R_MIPS_LO16       global_var

...

\end{lstlisting}

Podemos observar que o endereço da \IT{global\_var} será escrito diretamente \INS{LUI} e instruções \INS{LW} durante o carregamento do arquivo executável:
Os 16-bits altos da \IT{global\_var} vai para a primeira (\INS{LUI}), e os 16-bits mais baixos vão para a segunda (\INS{LW}).